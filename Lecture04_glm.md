# 第4回（90分）— 回帰分析の基礎（線形・ロジスティック・修正Poisson）  

この回は、回帰分析を「コマンド暗記」で終わらせず、**回帰係数の意味**と、実務でトラブルを起こしにくい **doファイルの設計**をセットで身につける回です。  
ここでは一旦**欠損値をこの回では直しません**。Stataは欠損がある観測を自動で除外するため、回帰を回すだけで **Nが減る**現象が起きます。これを敢て確認します。

---

## テーマ

**「回帰分析の基礎（線形・ロジスティック・修正Poisson）」**

---

## 到達目標

- 回帰係数（線形回帰）／オッズ比（ロジスティック）／リスク比（修正Poisson）の意味を説明できる  
- 粗解析モデルと調整モデルの違いを説明できる  
- 同じ共変量セットを `local` で管理し、doファイルを再利用できる  
- 欠損値により `e(N)` が減ることを確認し、「次回で直す」伏線を張れる  

---

## 研究仮説（この回の分析の軸）

- **曝露（Exposure）**：`dm`（糖尿病, 0/1）
- **アウトカム（Outcome）**：`cv_event`（心筋梗塞/脳卒中イベント, 0/1）
- **交絡候補（Covariates）**：  
  `age bmi sbp fev1 smk htn_tx fhx_cvd af ckd ra copd`

> 注意：本来、time-to-event変数があるなら、`cv_event` の解析は生存時間分析（KM/Cox）に寄せるのが自然です。  
> ただしこの回では「回帰の基本」を理解するため、まずは二値アウトカムの回帰（logistic / 修正Poisson）を通します。  
> 生存解析は第7回で正式に扱います。

---

# 0. この回の前提（欠損値の扱い）

この回では **欠損値を補完しません**。  
その代わり、次の2点を必ず実施します。

1) 欠損の存在を `misstable summarize` で確認する  
2) 回帰実行後に `e(N)` を確認し、「欠損により解析対象が減った」ことを体験する  

> これは意図的な設計です。  
> 解析を進めると勝手にNが減っていく“怖さ”を体験しておくことが、第5回（欠損値対応）の最大の導入になります。

---

# 1. 因果推論の超基礎（15分）

## 1.1 「交絡」とは何か

交絡（confounding）とは、ざっくり言えば

> 「曝露（dm）とアウトカム（cv_event）の両方に関係する第3の要因があるため、  
>  dm と cv_event の関係が“見かけ上”歪むこと」

です。

例として、年齢（`age`）を考えると分かりやすいです。

- 年齢が高いほど糖尿病（`dm`）が増える（`dm`と関連）
- 年齢が高いほど心血管イベント（`cv_event`）も増える（アウトカムと関連）

この場合、年齢を無視すると「dmが悪い」のではなく「年齢が高い」影響を dm を介して見てしまう可能性があります。
よりフォーマルには、DAGを用いて考えたり、mDCC(modified Disjunctive Cause Criterion)を元にして考えます。

---

## 1.2 「調整する」とは何をしているのか？

調整（adjustment）とは、

> 「曝露とアウトカムの関係をみるときに、交絡因子を“同じ条件にそろえる”試み」全般

です。

回帰モデルはそのための代表的な手法の1つで、**交絡候補をモデルに入れる**ことで、
「同じ年齢、同じBMI、同じ血圧…の人同士を比べたときの dm の差」を推定しようとします。

ただし注意点として、

- 入れるべき交絡因子を入れ忘れた → 交絡が残る  
- 本当は交絡でないもの（中間因子やコライダーなど）を入れた → 別のバイアスが起きる  
- 測定されていない交絡 → 調整できない  

などの限界があります。  
この回ではまず「回帰は調整の手法の1つ」という最小理解を押さえます。

---

# 2. 線形回帰

## 2.1 何をやるか

- **曝露**：`dm`
- **アウトカム**：`cv_time`（観察時間）
- **共変量**：`age bmi sbp fev1 smk htn_tx fhx_cvd af ckd ra copd`

ここで重要な補足：

> `cv_time` は本来、生存時間分析で扱う「観察時間」です。  
> 線形回帰で `cv_time` をアウトカムにする解析は、実務では通常推奨されません。  
> ただし、線形回帰は回帰係数の理解に最適なので、教材として実施します。

---

## 2.2 回帰係数の意味（線形回帰）

線形回帰 `regress` では、係数は次の意味になります。

- `dm` の係数：  
  「他の共変量が同じとき、糖尿病あり（dm=1）となし（dm=0）で、  
   `cv_time` の平均がどれだけ異なるか（年単位）」

---

# 3. ロジスティック回帰

## 3.1 何をやるか

- **曝露**：`dm`
- **アウトカム**：`cv_event`
- **共変量**：`age bmi sbp fev1 smk htn_tx fhx_cvd af ckd ra copd`

---

## 3.2 オッズ比の意味

ロジスティック回帰 `logit`（または `logistic`）では、係数はログオッズですが、通常は **オッズ比（OR）**として解釈します。

- `dm` のOR：  
  「他の共変量が同じとき、dm=1 の人のイベント発生オッズが dm=0 の何倍か」

注意点：

- オッズ比はリスク比ではありません  
- イベントが稀でないと、ORはRRより大きく見えやすい  

そのため、この回では次の修正Poisson回帰も扱います。

---

# 4. 修正Poisson回帰

## 4.1 何のためにやるか

二値アウトカム（`cv_event`）に対して、リスク比（RR）を直接解釈したい場合があります。  
その代表的な方法が「修正Poisson回帰（modified Poisson regression）」です。

Stataでは典型的に：

- `glm <outcome> <exposure> <confoundings>, family(poisson) link(log) vce(robust) eform`
- `poisson <outcome> <exposure> <confoundings>,robust irr`

で実装します。  
（二値アウトカムにPoissonを当て、ロバスト分散で推定する、という考え方）

---

## 4.2 リスク比の意味

- `dm` のRR：  
  「他の共変量が同じとき、dm=1 の人のイベント発生リスクが dm=0 の何倍か」
ただし、Stataでは（多分、他の統計ソフトでも）RRではなくIRR（incidence rate ratio）という表示になります。

---

# 5. doファイル設計

この回の技術的ゴールは、回帰を再利用しつつ書けるようになることです。  
そこで、共変量セットを `local` で管理し、粗解析と調整解析を同じ枠で回します。

- 共変量リストは `local covars` にまとめる  
- 粗解析：`dm` のみ  
- 調整解析：`dm` + `covars`  
- それぞれで `e(N)` を必ず確認する（欠損の影響を可視化）

---

# 6. 04_models_glm.do

```stata
****************************************************
* 04_models_glm.do
* 役割：線形回帰・ロジスティック回帰・修正Poisson回帰
* 重要：欠損補完はこの回では行わない（e(N)を確認する）
****************************************************

use "$CLEAN\df01_clean.dta", clear

capture log close
log using "$LOG/session4_models.log", text replace
di "=== Session 4: Regression basics ==="

*--------------------------------------------------*
* 0) 欠損の確認（補完はしない）
*--------------------------------------------------*
misstable summarize bmi sbp fev1

*--------------------------------------------------*
* 1) 変数セット（他の解析をするときにはこのセクションだけ変更すればOK）
*--------------------------------------------------*
* 曝露変数
local expv dm

* アウトカム 
local outv_cont cv_time
local outv_bin  cv_event


* 交絡候補
local covars age bmi sbp fev1 smk htn_tx fhx_cvd af ckd ra copd

*--------------------------------------------------*
* 2) 線形回帰：アウトカム=cv_time（教材用）
*--------------------------------------------------*
di "---- Linear regression (Outcome: cv_time) ----"

* 粗解析（dmのみ）
regress `outv_cont' `expv'
di "N used (crude): " e(N)

* 調整解析（dm + covars）
regress `outv_cont' `expv' `covars'
di "N used (adjusted): " e(N)

*--------------------------------------------------*
* 3) ロジスティック回帰：アウトカム=cv_event（OR）
*--------------------------------------------------*
di "---- Logistic regression (Outcome: cv_event) ----"

* 粗解析（OR）
logistic `outv_bin' `expv'
di "N used (crude): " e(N)

* 調整解析（OR）
logistic `outv_bin' `expv' `covars'
di "N used (adjusted): " e(N)

*--------------------------------------------------*
* 4) 修正Poisson回帰：アウトカム=cv_event（RR）
*--------------------------------------------------*
di "---- Modified Poisson regression (Outcome: cv_event) ----"

* 粗解析（RR）
poisson `outv_bin' `expv', robust irr
di "N used (crude): " e(N)

* 調整解析（RR）
poisson `outv_bin' `expv' `covars', robust irr
di "N used (adjusted): " e(N)

*--------------------------------------------------*
* 5) 欠損によるNの減少を可視化
*--------------------------------------------------*
di "---- What reduced N? (missingness check) ----"
* 回帰に使った変数のどれかが欠損すると落ちる
egen miss_any = rowmiss(dm `covars' cv_time cv_event)
tab miss_any

di "=== Session 4 completed ==="
log close
```


