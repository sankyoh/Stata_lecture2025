
# 第3回— 記述統計と Table 1

この回は、回帰分析や生存解析に入る前に「データを語れる」状態を作る回です。  
実務では、解析手法そのものよりも **“データの理解不足”** が原因で、誤解・手戻り・査読での指摘が起きます。

今回の研究仮説は「`dm` が `cv_event` に与える影響」ですが、いきなり回帰に進むと次のような事故が起きがちです。

- `dm=1` 群がどれくらいの人数か分からない
- `cv_event` の発生率が不明なままモデルを組む
- `bmi`, `sbp`, `fev1` に欠損があるのに気づかない（または無視してしまう）
- 外れ値フラグ（`age_outlier` など）がどれくらいあるか把握していない

そこで第3回では、**Table 1（ベースラインの記述統計表）を作る**ことを通じて、

1) データの全体像  
2) 変数の分布と欠損  
3) 群（曝露／非曝露）間の違いの見え方  

を整理します。

> 注意：この回では「因果推論として正しい推定」には踏み込みません。  
> Table 1 は“調整前の記述”であり、因果効果の結論を出す道具ではありません。  
> ただし、以後の回（回帰・PS・生存解析）を安全に進めるための必須工程です。

---

## テーマ

**「記述統計と Table 1」**

---

## 到達目標

- 解析の前に「データを語れる」
- 変数の型に応じた要約ができる
- 欠損・外れ値フラグを含めた Table 1 を作成できる
- `dtable` で作表し、Excelに出力できる

---

## 変数（今回の前提）

### ID・群分け（アウトカム）
- `patient_id`
- `cv_event`：心筋梗塞/脳卒中イベント（0/1）
- `cv_time`：イベントまたは打ち切りまでの時間（年）

### 主要曝露（研究仮説）
- `dm`：糖尿病（0/1） ← 仮説の中心

### 共変量（Table 1 に載せる）
- 連続：`age`, `bmi`, `sbp`, `fev1`, `cv_time`
- 二値：`smk`, `htn_tx`, `fhx_cvd`, `af`, `ckd`, `ra`, `copd`

### Sanity check フラグ（02_clean.doで作成済みの変数）
- `age_outlier`, `bmi_outlier`, `sbp_outlier`, `cv_time_outlier`

### Missing フラグ （02_clean.doで作成済みの変数）
- `miss_bmi`, `miss_sbp`, `miss_fev1`, `miss_cv_time`

> 欠損がある変数：`bmi`, `sbp`, `fev1`  
> この回では欠損補完はしません（第5回で扱う予定）。  
> ただし Table 1 には欠損数を表示し、現状を“見える化”します。

---

# 1. 記述統計の基本（導入）

記述統計は、データの「要約」です。  
ただし、要約の方法は変数の型によって変わります。

- **連続変数**：平均・SD、または中央値・四分位範囲（IQR）
- **カテゴリ変数（二値含む）**：人数（%）

さらに Table 1 では、通常は **曝露群（ここでは `dm` の有無）で層別**して示します。  
つまり Table 1 は次の2段階の要約です。

1) 全体像を要約する  
2) 群別に要約し、違いを可視化する  

---

# 2. Stataコマンド

## 2.1 まずは「素の確認」：summarize / tabulate / table

Table 1 をいきなり作る前に、最低限の sanity check（統計的な意味での確認）をします。

- `summarize`：連続変数の基本統計量
- `tabulate`：カテゴリ変数の分布（欠損も確認）
- `table`：複数変数のまとめ（表示用）

---

# 3. 03_table1.do（サンプル：講義用テキスト＋コード）

この do ファイルは「Table 1 を作って Excel に出力する」が主目的です。  
ただし、出力に入る前に **“確認 → 作表”** の順を守ります。

> ポイント：  
> - `dtable` を使うと、Table 1 の作成が簡潔になる  
> - ただし、dtableに頼りすぎると「何をしているか」が不透明になる  
> - なので、先に `summarize` / `tabulate` で確認してから `dtable` へ進む

---

```stata
****************************************************
* 03_table1.do
* 役割：記述統計の確認と Table 1 作成（Excel出力）
* データ：data_clean\cardio_clean.dta を前提
****************************************************

****************************************************
* 0) 準備
****************************************************
cap log close
log using "$LOG\log_03_tale1.smcl", replace
di "=== Session 3: Table 1 ==="

* 読込みデータファイルと書出しデータファイル
local read_file   "$CLEAN\df01_clean.dta"
local write_file1 "$OUT/table1_1.xlsx"
local write_file2 "$OUT/table1_2.xlsx"

* 変数の定義
local expv dm

* des_vars = dtableで利用する：記述統計量で示す変数リスト（i.もつける）
local des_vars ///
	age bmi sbp fev1 cv_time ///
    i.smk i.htn_tx i.fhx_cvd i.af i.ckd i.ra i.copd i.cv_event ///
    i.age_outlier i.bmi_outlier i.sbp_outlier i.cv_time_outlier ///
	i.miss_bmi i.miss_sbp i.miss_fev1 i.miss_cv_time

use "`read_file'", clear

****************************************************
* 1) 記述統計（連続変数）
****************************************************
* 記述統計量を確認する
su age bmi sbp fev1 cv_time, detail

****************************************************
* 2) カテゴリ変数
****************************************************
* 二値変数の分布を確認する
foreach v in cv_event dm smk htn_tx fhx_cvd af ckd ra copd {
    tab `v', missing
}

****************************************************
* 3) 欠損の可視化
****************************************************
misstable summarize bmi sbp fev1

****************************************************
* 4) Table 1 を作る（dtable）
****************************************************
* dtable は Stata 17+ の機能。Table 1 を一気に作るのに便利。
* このコースでは連続変数について「平均±SDのパターン」と「中央値[IQR]のパターン」を両方作る。


****************************************************
* 4A) 平均±SD 版（欠損数も表示）
****************************************************
* iqr_vars = dtableで利用する：記述統計の表でIQRを示す変数を示すローカルマクロ
local iqr_vars // 空白にしている（つまり、全部の連続変数を平均と標準偏差で示す）

dtable `des_vars', ///
		by(`expv', nototals notests missing) ///
		column(by(hide)) /// 
		sample(, place(seplabels)) ///
		///
		define(iqi = q1 q3, delimiter(", ")) ///
		sformat("[%s]" iqi) /// 
		///	
		nformat(%16.2fc mean sd q1 q2 q3) ///
		continuous(, test(regress)) ///
		continuous(`iqr_vars', statistic(q2 iqi)) ///
		factor(,test(pearson)) ///
		///
		note(Mean(SD) or N(%)) ///
		note(Median[IQR]) ///
		///
		export("`write_file1'", as(xlsx) replace)

****************************************************
* 4B) 中央値[IQR] 版（欠損数も表示）
****************************************************
* iqr_vars = dtableで利用する：記述統計の表でIQRを示す変数を示すローカルマクロ
local iqr_vars age bmi sbp fev1 cv_time // 連続変数を全部指定している（つまり、全部の連続変数を中央値とIQRで示す）

dtable `des_vars', ///
		by(`expv', nototals notests missing) ///
		column(by(hide)) /// 
		sample(, place(seplabels)) ///
		///
		define(iqi = q1 q3, delimiter(", ")) ///
		sformat("[%s]" iqi) /// 
		///	
		nformat(%16.2fc mean sd q1 q2 q3) ///
		continuous(, test(regress)) ///
		continuous(`iqr_vars', statistic(q2 iqi)) ///
		factor(,test(pearson)) ///
		///
		note(Mean(SD) or N(%)) ///
		note(Median[IQR]) ///
		///
		export("`write_file2'", as(xlsx) replace) ///

di "=== Table 1 exported to: `write_file1' and `write_file2' ==="

log close
````

---

# 4. Table 1 の読み方（講義用の文章）

## 4.1 Table 1 で何を見るか

Table 1 は、研究の「登場人物紹介」です。
ここで見るべきは主に4つです。

1. **サンプルサイズ**

   * 全体のN
   * `cv_event=1` がどれくらいあるか（イベント率）
2. **主要曝露（dm）の分布**

   * `dm=1` は何人か
   *  dm=1群でのイベント数が多いか少ないか（あくまで粗い比較）
3. **共変量の偏り**

   * 年齢、血圧、BMI、喫煙などがイベント群で偏っていないか
4. **欠損の偏り**

   * `bmi`, `sbp`, `fev1` の欠損がどの程度か
   * 欠損が群で偏っていないか（偏りがあると危険）

5. **外れ値の偏り**
* 連続変数の外れ値が群で偏っていないか（偏りがあると危険）

> Table 1 で群間差があることは「交絡の可能性」を示します。
> ただし、差があるからといって「因果」ではありません。
> 因果推論は第4回以降（回帰、PS、サバイバル）で扱う予定です。

---

## 4.2 平均±SD と 中央値[IQR] をどう使い分けるか

* 分布が概ね対称（正規っぽい）なら、平均±SDが分かりやすい
* 歪んでいる（外れ値が多い）なら、中央値[IQR]が頑健

この回では両方出力しますが、実務では次のように考えます。

* `age`：平均でも中央値でも良いことが多い
* `bmi`, `sbp`：外れ値や欠損が絡みやすいので中央値が安心な場合あり
* `fev1`：欠損の扱いが重要（中央値も検討）

---

# 5. 欠損の扱い（第5回への伏線）

今回のデータでは `bmi`, `sbp`, `fev1` に欠損があります。
第3回では **欠損を直しません**。理由は2つです。

1. 欠損補完は「原理と手順」を体系的に扱う必要がある（第5回）
2. 第4回で回帰をするとき、Stataが欠損行を自動で落とすことで   **“Nが減る”ことをあえて体験**するため

第3回でやるべきことは、

* 欠損がある事実を可視化する
* 欠損がどの変数にあるか把握する
* 欠損率がどれくらいか示す

ここまでです。

---

# 6. やること

## 課題1：Table 1 を2種類作ってExcelに出力する

* すべての連続変数を「平均値、標準偏差で示す」版と「中央値、IQRで示す」が作られることを確認する。
* 【応用】必要な変数のみを「中央値、IQR」で示し、他は「平均値、標準偏差」で示す版を作る。

## 課題2：dm と cv_event の関係を「言葉で説明」する

* 例：「 dm=1の群ではcv_eventありの割合が高いように見える」
* ただし「因果とは言えない」と必ず書く（交絡があるため）

## 課題3：欠損はどこにあるか、何％か

* `misstable summarize bmi sbp fev1` の結果をメモする
* 次回以降の解析で「Nが減る」原因になることを理解する

---

# 7. まとめ

第3回では、解析に入る前に「データを語れる」状態を作りました。

* `summarize` / `tab` で分布と欠損を確認
* `dtable` で Table 1 を作成
* Excel出力して “成果物” として残す
* `dm → cv_event` の粗い関係を眺め、次回の回帰へつなげる

# 8. ここで語らなかったこと
tabstatを用いるとさらにいろいろな統計量を算出できます。必要に応じて追加します。
